#include "/home/terra/TerraNG/terra/TerraNet.defs"

var ushort nodeId = getNodeId();

pktype usrMsg from radioMsg with
	var ubyte[4]  d8;
	var ushort[4] d16;
	var ulong[2]  d32;
end

var usrMsg sndData;
var usrMsg recData;
var usrMsg msgRadio;

var ubyte stat;

sndData.source = nodeId;
sndData.d32[0] = 0;

if nodeId == 21 then
	sndData.target = 12;
else
	if nodeId == 11 then
		sndData.target = 1;
	else
		sndData.target = nodeId - 1;
	end
end

par do
	loop do
		emit REQ_TEMP();
		sndData.d16[0] = await TEMP;
		stat = qPut(sndData);
		await 10s;
	end
with
	loop do
		recData = await RECEIVE;
		stat = qPut(recData);
	end
with
	loop do
		if qSize() == 0 then
			await Q_READY;
		else
			stat = qGet(msgRadio);
			emit SEND(msgRadio);
			await SEND_DONE;
		end
	end
end
